if (NOT DEFINED ILC)
    message(FATAL_ERROR "ILC not set")
endif ()
if (NOT DEFINED BAS_FILE)
    message(FATAL_ERROR "BAS_FILE not set")
endif ()
if (NOT DEFINED GOLDEN)
    message(FATAL_ERROR "GOLDEN not set")
endif ()
execute_process(COMMAND ${ILC} front basic -emit-il ${BAS_FILE} OUTPUT_VARIABLE out RESULT_VARIABLE res)
if (NOT res EQUAL 0)
    message(FATAL_ERROR "emit-il failed")
endif ()
# Normalize Windows line endings
string(REPLACE "\r\n" "\n" out "${out}")
string(REPLACE "\r" "\n" out "${out}")
file(READ ${GOLDEN} expected)
string(REPLACE "\r\n" "\n" expected "${expected}")
string(REPLACE "\r" "\n" expected "${expected}")
## Normalize IL version to avoid test churn on version bumps.
string(REGEX REPLACE "^il [0-9]+\\.[0-9]+\\.[0-9]+" "il VERSION" out "${out}")
string(REGEX REPLACE "^il [0-9]+\\.[0-9]+\\.[0-9]+" "il VERSION" expected "${expected}")
## Normalize legacy rt_* aliases to canonical Viper.* names in both expected and output.
set(_aliases rt_print_str;rt_print_i64;rt_print_f64;rt_str_len;rt_str_substr;rt_trap;rt_str_concat;rt_input_line;rt_to_int;rt_to_double;rt_parse_int64;rt_parse_double;rt_int_to_str;rt_f64_to_str;rt_str_split_fields;rt_str_i16_alloc;rt_str_i32_alloc;rt_str_f_alloc)
set(_canon Viper.Terminal.PrintStr;Viper.Terminal.PrintI64;Viper.Terminal.PrintF64;Viper.String.get_Length;Viper.String.Substring;Viper.Diagnostics.Trap;Viper.String.Concat;Viper.Terminal.ReadLine;Viper.Convert.ToInt;Viper.Convert.ToDouble;Viper.Parse.Int64;Viper.Parse.Double;Viper.Convert.ToString_Int;Viper.Convert.ToString_Double;Viper.String.SplitFields;Viper.String.FromI16;Viper.String.FromI32;Viper.String.FromSingle)
list(LENGTH _aliases _n)
math(EXPR _last "${_n} - 1")
foreach (i RANGE 0 ${_last})
    list(GET _aliases ${i} _a)
    list(GET _canon ${i} _c)
    string(REPLACE "@${_a}(" "@${_c}(" out "${out}")
    string(REPLACE "@${_a}(" "@${_c}(" expected "${expected}")
    string(REPLACE "extern @${_a}" "extern @${_c}" out "${out}")
    string(REPLACE "extern @${_a}" "extern @${_c}" expected "${expected}")
endforeach ()
## Extern declaration order may differ across platforms (hash map iteration).
## Compare extern lines sorted, then compare the remaining IL body verbatim.
string(REGEX MATCHALL "extern @[^\n]+" _out_externs "${out}")
string(REGEX MATCHALL "extern @[^\n]+" _exp_externs "${expected}")
list(SORT _out_externs)
list(SORT _exp_externs)
if (NOT "${_out_externs}" STREQUAL "${_exp_externs}")
    message(FATAL_ERROR "Extern declarations mismatch\nExpected:\n${_exp_externs}\nGot:\n${_out_externs}")
endif ()
string(REGEX REPLACE "extern @[^\n]+\n?" "" _out_body "${out}")
string(REGEX REPLACE "extern @[^\n]+\n?" "" _exp_body "${expected}")
if (NOT "${_out_body}" STREQUAL "${_exp_body}")
    message(FATAL_ERROR "IL body mismatch\nExpected:\n${_exp_body}\nGot:\n${_out_body}")
endif ()
