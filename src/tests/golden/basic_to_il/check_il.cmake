if (NOT DEFINED ILC)
    message(FATAL_ERROR "ILC not set")
endif ()
if (NOT DEFINED BAS_FILE)
    message(FATAL_ERROR "BAS_FILE not set")
endif ()
if (NOT DEFINED GOLDEN)
    message(FATAL_ERROR "GOLDEN not set")
endif ()
execute_process(COMMAND ${ILC} front basic -emit-il ${BAS_FILE} OUTPUT_VARIABLE out RESULT_VARIABLE res)
if (NOT res EQUAL 0)
    message(FATAL_ERROR "emit-il failed")
endif ()
# Normalize Windows line endings
string(REPLACE "\r\n" "\n" out "${out}")
string(REPLACE "\r" "\n" out "${out}")
file(READ ${GOLDEN} expected)
string(REPLACE "\r\n" "\n" expected "${expected}")
string(REPLACE "\r" "\n" expected "${expected}")
## Normalize IL version to avoid test churn on version bumps.
string(REGEX REPLACE "^il [0-9]+\\.[0-9]+\\.[0-9]+" "il VERSION" out "${out}")
string(REGEX REPLACE "^il [0-9]+\\.[0-9]+\\.[0-9]+" "il VERSION" expected "${expected}")
## Normalize legacy rt_* aliases to canonical Viper.* names in both expected and output.
set(_aliases rt_print_str;rt_print_i64;rt_print_f64;rt_len;rt_substr;rt_trap;rt_concat;rt_input_line;rt_to_int;rt_to_double;rt_parse_int64;rt_parse_double;rt_int_to_str;rt_f64_to_str;rt_split_fields;rt_str_i16_alloc;rt_str_i32_alloc;rt_str_f_alloc;rt_str_d_alloc)
set(_canon Viper.Terminal.PrintStr;Viper.Terminal.PrintI64;Viper.Terminal.PrintF64;Viper.String.get_Length;Viper.String.Substring;Viper.Diagnostics.Trap;Viper.String.Concat;Viper.Terminal.ReadLine;Viper.Convert.ToInt;Viper.Convert.ToDouble;Viper.Parse.Int64;Viper.Parse.Double;Viper.String.FromInt;Viper.String.FromDouble;Viper.String.SplitFields;Viper.String.FromI16;Viper.String.FromI32;Viper.String.FromSingle;Viper.String.FromDoublePrecise)
list(LENGTH _aliases _n)
math(EXPR _last "${_n} - 1")
foreach (i RANGE 0 ${_last})
    list(GET _aliases ${i} _a)
    list(GET _canon ${i} _c)
    string(REPLACE "@${_a}(" "@${_c}(" out "${out}")
    string(REPLACE "@${_a}(" "@${_c}(" expected "${expected}")
    string(REPLACE "extern @${_a}" "extern @${_c}" out "${out}")
    string(REPLACE "extern @${_a}" "extern @${_c}" expected "${expected}")
endforeach ()
if (NOT out STREQUAL expected)
    message(FATAL_ERROR "IL mismatch\nExpected: ${expected}\nGot: ${out}")
endif ()
